buildscript {
    ext.kotlin_version = '1.2.21'
    ext.fuel_version = '1.12.0'
    ext.gson_version = '2.8.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


group 'Duke'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

/* NOTE: to use gradle and send in argument have to do this:

  $ ./gradlew widgetsCheck -Pdate=2018-02-15

*/
task widgetsCheck(type: JavaExec) {
    systemProperty "configuration.directory", "conf"

    if(project.hasProperty('date')){
        args(date)
    }

    main = "duke.WidgetsKt"
    classpath = sourceSets.main.runtimeClasspath
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    ktlint "com.github.shyiko:ktlint:0.15.0"
    compile "com.google.code.gson:gson:$gson_version"
    compile "com.github.kittinunf.fuel:fuel:$fuel_version" //for JVM
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-reflect
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.2.21'

    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.2'
    //compile group: 'org.bytedeco', name: 'javacv-platform', version: '1.3.3'

    compile 'com.natpryce:konfig:1.6.1.0'

    // https://mvnrepository.com/artifact/org.springframework/spring-jdbc
    compile group: 'org.springframework', name: 'spring-jdbc', version: '5.0.4.RELEASE'

    //https://dzone.com/articles/jdbi-a-nice-spring-jdbc-alternative

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'

    compile 'org.jdbi:jdbi3-bom:3.0.1'
    compile 'org.jdbi:jdbi3-kotlin:3.0.1'
    compile 'org.jdbi:jdbi3-kotlin-sqlobject:3.0.1'
    compile 'org.jdbi:jdbi3-sqlobject:3.0.1'
    compile 'org.jdbi:jdbi3-postgres:3.0.1'

    compile group: 'com.h2database', name: 'h2', version: '1.4.196'

    // https://mvnrepository.com/artifact/com.h2database/h2
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.196'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


kotlin {
    experimental {
        coroutines 'enable'
    }
}

// http://blog.getsandbox.com/2014/08/26/reduce-gradle-memory/
// usage:
// 
// > gradle createdeps
//
// then run any class in project (see run.sh):
// 
// > ./run.sh duke.WidgetsKt

task createdeps() {
    def pathFile = new File(".classpath")
    pathFile.write("export CLASSPATH=" + sourceSets.main.runtimeClasspath.asPath);
}
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
